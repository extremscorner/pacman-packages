# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=brotli
pkgname="ppc-lib${_pkgname}"
pkgver=1.2.0
pkgrel=6
pkgdesc='A generic-purpose lossless compression algorithm.'
arch=('any')
url='https://github.com/google/brotli'
license=('MIT')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'libogc2-dkp-toolchain-vars' 'patch')
options=(!strip libtool staticlibs)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
		"${_pkgname}-${pkgver}.patch")
sha256sums=('816c96e8e8f193b40151dad7e8ff37b1221d019dbcb9c35cd3fadbfe6477dfec'
			'f2249e6aa41b7c6b3e557036324ee4ebcd3240e1b6c4d582666e55480e4f4039')

prepare() {
	patch -Np 0 -i "${_pkgname}-${pkgver}.patch"
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DBROTLI_BUILD_TOOLS=OFF -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}"
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	rm -r "${pkgdir}${PORTLIBS_PREFIX}/share"
	install -Dm 644 LICENSE -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
