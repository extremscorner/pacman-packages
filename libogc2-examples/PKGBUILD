# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=({gamecube,wii}-examples)
pkgname=libogc2-examples
pkgver=20251115
pkgrel=1
pkgdesc='C library for GameCube and Wii targeting devkitPPC. (examples)'
arch=('any')
url='https://github.com/extremscorner/libogc2'
license=('custom')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC' 'gamecube-tools-git' 'libogc2' 'libogc2-libfat' 'ppc-libmad' 'ppc-libvorbisidec')
optdepends=('devkitPPC-gdb: for debugging'
			'libogc2-cmake: for CMake examples'
			'wiiload: for run target')
options=(!strip libtool staticlibs)

for i in ${!_pkgname[@]}; do
	source+=("${_pkgname[i]}-${pkgver}.tar.gz::https://github.com/extremscorner/${_pkgname[i]}/archive/refs/tags/v${pkgver}.tar.gz")
done

sha256sums=('e16b544150e9172a42de0345800f774400f70da021c5f54cd13f8d4526e8de4c'
			'53094099ee00b050cd394ecb5fa0a1571241d90c4704ed60b1c229bdb29b85b7')

prepare() {
	for i in ${!_pkgname[@]}; do
		find "${_pkgname[i]}-${pkgver}" -name '.gitignore' -delete
	done
}

package() {
	for i in ${!_pkgname[@]}; do
		local platform=${_pkgname[i]%-examples}
		mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/libogc2"
		cp -r "${_pkgname[i]}-${pkgver}" "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/libogc2/examples"
	done
}
