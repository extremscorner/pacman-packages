# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=({gamecube,wii}-examples)
pkgname=libogc2-examples
pkgver=20250824
pkgrel=1
pkgdesc='C library for GameCube and Wii targeting devkitPPC. (examples)'
arch=('any')
url='https://github.com/extremscorner/libogc2'
license=('custom')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC' 'gamecube-tools-git' 'libogc2' 'libogc2-libfat' 'ppc-libmad' 'ppc-libvorbisidec')
optdepends=('devkitPPC-gdb: for debugging'
			'libogc2-cmake: for CMake examples'
			'wiiload: for run target')
options=(!strip libtool staticlibs)

for i in ${!_pkgname[@]}; do
	source+=("${_pkgname[i]}-${pkgver}.tar.gz::https://github.com/extremscorner/${_pkgname[i]}/archive/refs/tags/v${pkgver}.tar.gz")
done

sha256sums=('7e7eb40cb7a1963975a6774107086b0bf2f3ab7f32d9bcde5f7d5a9ea5bc1a5d'
			'490589fbf335d8799de7890af70f800b666622431a17d5662f259b6a28c36df5')

prepare() {
	for i in ${!_pkgname[@]}; do
		find "${_pkgname[i]}-${pkgver}" -name '.gitignore' -delete
	done
}

package() {
	mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/share/doc/libogc2/examples"
	for i in ${!_pkgname[@]}; do
		cp -r "${_pkgname[i]}-${pkgver}" "${pkgdir}${DEVKITPRO}/libogc2/share/doc/libogc2/examples/${_pkgname[i]%-examples}"
	done
}
