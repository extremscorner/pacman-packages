# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=({gamecube,wii}-examples)
pkgname=libogc2-examples
pkgver=20260112
pkgrel=1
pkgdesc='C library for GameCube and Wii targeting devkitPPC. (examples)'
arch=('any')
url='https://github.com/extremscorner/libogc2'
license=('custom')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC' 'gamecube-tools-git' 'libogc2' 'libogc2-libfat' 'ppc-libmad' 'ppc-libvorbisidec')
optdepends=('devkitPPC-gdb: for debugging'
			'libogc2-cmake: for CMake examples'
			'wiiload: for run target')
options=(!strip libtool staticlibs)

for i in ${!_pkgname[@]}; do
	source+=("${_pkgname[i]}-${pkgver}.tar.gz::https://github.com/extremscorner/${_pkgname[i]}/archive/refs/tags/v${pkgver}.tar.gz")
done

sha256sums=('4067c967493d0e862de5f172af61df6d8ac32a4cf2941dc951c912a6266685c7'
			'c70b48e432266f02d8077bb4216cba3569a1bbfdf75c06d6a2ad715d71abab7d')

prepare() {
	for i in ${!_pkgname[@]}; do
		find "${_pkgname[i]}-${pkgver}" -name '.gitignore' -delete
	done
}

package() {
	for i in ${!_pkgname[@]}; do
		local platform=${_pkgname[i]%-examples}
		mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/libogc2"
		cp -r "${_pkgname[i]}-${pkgver}" "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/libogc2/examples"
	done
}
