# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=({gamecube,wii}-examples)
pkgname=libogc2-examples
pkgver=20260207
pkgrel=1
pkgdesc='C library for GameCube and Wii targeting devkitPPC. (examples)'
arch=('any')
url='https://github.com/extremscorner/libogc2'
license=('custom')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC' 'gamecube-tools-git' 'libogc2' 'libogc2-libfat' 'ppc-libmad' 'ppc-libvorbisidec')
optdepends=('devkitPPC-gdb: for debugging'
			'libogc2-cmake: for CMake examples'
			'wiiload: for run target')
options=(!strip libtool staticlibs)

for i in ${!_pkgname[@]}; do
	source+=("${_pkgname[i]}-${pkgver}.tar.gz::https://github.com/extremscorner/${_pkgname[i]}/archive/refs/tags/v${pkgver}.tar.gz")
done

sha256sums=('fcc885629b56f12250cd347b4c0ef4cb6dabf15820d1d87a4ba2002f9abf12b1'
			'ced6fde59f359df11d85323b25d1a1d37409c6697b6d6f39c54038213a78848e')

prepare() {
	for i in ${!_pkgname[@]}; do
		find "${_pkgname[i]}-${pkgver}" -name '.gitignore' -delete
	done
}

package() {
	for i in ${!_pkgname[@]}; do
		local platform=${_pkgname[i]%-examples}
		mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/libogc2"
		cp -r "${_pkgname[i]}-${pkgver}" "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/libogc2/examples"
	done
}
