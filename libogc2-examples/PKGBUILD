# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=({gamecube,wii}-examples)
pkgname=libogc2-examples
pkgver=20250607
pkgrel=1
pkgdesc='C library for GameCube and Wii targeting devkitPPC. (examples)'
arch=('any')
url='https://github.com/extremscorner/libogc2'
license=('custom')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC' 'gamecube-tools-git' 'libogc2' 'libogc2-libfat' 'ppc-libmad' 'ppc-libvorbisidec')
optdepends=('devkitPPC-gdb: for debugging'
			'libogc2-cmake: for CMake examples'
			'wiiload: for run target')
options=(!strip libtool staticlibs)

for i in ${!_pkgname[@]}; do
	source+=("${_pkgname[i]}-${pkgver}.tar.gz::https://github.com/extremscorner/${_pkgname[i]}/archive/refs/tags/v${pkgver}.tar.gz")
done

sha256sums=('225f5d8f37b4c3c215054262c39fc089c7bf8b09da2af8e31d51117d4965c0c9'
			'b58bac7ebef47cea10aafeb1fe6035562a84f1636b03d4669014c916d97a191e')

prepare() {
	for i in ${!_pkgname[@]}; do
		find "${_pkgname[i]}-${pkgver}" -name '.gitignore' -delete
	done
}

package() {
	mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/share/doc/libogc2/examples"
	for i in ${!_pkgname[@]}; do
		cp -r "${_pkgname[i]}-${pkgver}" "${pkgdir}${DEVKITPRO}/libogc2/share/doc/libogc2/examples/${_pkgname[i]%-examples}"
	done
}
