# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=opusfile
pkgname="ppc-${_pkgname}"
pkgver=0.12
pkgrel=3
pkgdesc='Library for opening, seeking, and decoding .opus files.'
arch=('any')
url='https://www.opus-codec.org/'
license=('BSD-3-Clause')
groups=('ppc-portlibs')
depends=('devkitPPC' 'ppc-libogg' 'ppc-libopus')
makedepends=('libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("https://ftp.osuosl.org/pub/xiph/releases/opus/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('118d8601c12dd6a44f52423e68ca9083cc9f2bfe72da7a8c1acb22a80ae3550b')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi --enable-fixed-point --disable-examples --disable-doc
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" make install
	rm -r "${pkgdir}${PORTLIBS_PREFIX}/share"
	install -Dm 644 COPYING -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
