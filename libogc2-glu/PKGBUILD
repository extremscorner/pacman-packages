# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=glu
pkgname="libogc2-${_pkgname}"
pkgver=9.0.3
pkgrel=1
pkgdesc='Mesa OpenGL utility library.'
arch=('any')
url='https://gitlab.freedesktop.org/mesa/glu'
license=('SGI-B-1.1' 'SGI-B-2.0')
groups=('gamecube-portlibs' 'wii-portlibs')
depends=('devkitPPC' 'libogc2-opengx')
makedepends=('gamecube-pkg-config' 'libogc2-dkp-toolchain-vars' 'meson' 'wii-pkg-config')
provides=({gamecube,wii}-${_pkgname})
conflicts=({gamecube,wii}-${_pkgname})
options=(!strip libtool staticlibs)
source=("https://archive.mesa3d.org/glu/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('bd43fe12f374b1192eb15fe20e45ff456b9bc26ab57f0eee919f96ca0f8a330f')

build() {
	for platform in gamecube wii; do
		cd "${srcdir}/${_pkgname}-${pkgver}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		meson env2mfile -o meson.cross --cross --system=bare --subsystem=bare --kernel=none --cpu=ppc --cpu-family=ppc --endian=big
		meson setup --prefix="${PORTLIBS_PREFIX}" --buildtype=plain --default-library=static --cross-file=meson.cross build-${platform}
		meson compile -C build-${platform}
	done
}

package() {
	for platform in gamecube wii; do
		cd "${srcdir}/${_pkgname}-${pkgver}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		DESTDIR="${pkgdir}" meson install -C build-${platform}
	done
}
