# Maintainer: Extrems <extrems@extremscorner.org>
# Contributor: WinterMute <davem@devkitpro.org>
# Contributor: fincs <fincs@devkitpro.org>

pkgname=libogc2-cmake
pkgver=20251128
pkgrel=1
pkgdesc='CMake support files for libogc(2).'
arch=('any')
url='https://github.com/extremscorner/libogc2'
groups=('gamecube-dev' 'wii-dev')
depends=('devkitppc-cmake' 'gamecube-cmake' 'gamecube-tools' 'libogc2-pkg-config' 'wii-cmake')
provides=('ogc-cmake')
conflicts=('ogc-cmake')
source=('libogc.cmake'
		'ogc-common.cmake'
		'powerpc-eabi-cmake.in')
sha256sums=('3f6232ab630fd51b182b486a3d295b5d5082729aa83eec08c34cbbfb6cf162f6'
			'4db98ded9ea24f82180b9c0e7453745097f7d7d5c2a9f859b1809de7dbd4a3e2'
			'58ff8d4c59ab071cc564752afd5426ba068fc3b26b3b33ab73c833cb680cec8f')

package() {
	install -Dm 644 ogc-common.cmake -t "${pkgdir}${DEVKITPRO}/cmake"
	install -Dm 644 libogc.cmake -t "${pkgdir}${DEVKITPRO}/cmake/Platform"
	for Platform in GameCube Wii; do
		sed "s/@PLATFORM@/${Platform}/g" powerpc-eabi-cmake.in > powerpc-eabi-cmake
		install -Dm 755 powerpc-eabi-cmake -t "${pkgdir}${DEVKITPRO}/libogc2/${Platform,,}/bin"
	done
}
