# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libogg
pkgname="ppc-${_pkgname}"
pkgver=1.3.6
pkgrel=6
pkgdesc='Ogg bitstream and framing library.'
arch=('any')
url='https://www.xiph.org/ogg/'
license=('BSD-3-Clause')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("https://ftp.osuosl.org/pub/xiph/releases/ogg/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('5c8253428e181840cd20d41f3ca16557a9cc04bad4a3d04cce84808677fa1061')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DBUILD_TESTING=OFF -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" -DINSTALL_DOCS=OFF
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 ogg.m4 -t "${pkgdir}${PORTLIBS_PREFIX}/share/aclocal"
	install -Dm 644 COPYING -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
