# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libogg
pkgname="ppc-${_pkgname}"
pkgver=1.3.6
pkgrel=1
pkgdesc='Ogg bitstream and framing library.'
arch=('any')
url='https://www.xiph.org/ogg/'
license=('BSD-3-Clause')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'devkitppc-cmake' 'dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("https://ftp.osuosl.org/pub/xiph/releases/ogg/${_pkgname}-${pkgver}.tar.xz"
		"${_pkgname}-${pkgver}.patch")
sha256sums=('5c8253428e181840cd20d41f3ca16557a9cc04bad4a3d04cce84808677fa1061'
			'ab18ccfed9fd419ed61bdac36863862a867d12fd93a8848b881ab19fdca4e402')

prepare() {
	patch -Np 0 -i "${_pkgname}-${pkgver}.patch"
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/ppcvars.sh"
	powerpc-eabi-cmake -B build -DBUILD_TESTING=OFF -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" -DINSTALL_DOCS=OFF
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 COPYING -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
