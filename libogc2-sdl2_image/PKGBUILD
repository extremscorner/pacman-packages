# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=SDL2_image
pkgname=(libogc2-${_pkgname,,}{,-full})
pkgver=2.8.8
pkgrel=7
pkgdesc='A simple library to load images of various formats as SDL surfaces. (Version 2)'
arch=('any')
url='https://github.com/libsdl-org/SDL_image'
license=('Zlib')
groups=('gamecube-portlibs' 'gamecube-sdl2-libs' 'wii-portlibs' 'wii-sdl2-libs')
depends=('devkitPPC' 'libogc2-sdl2' 'ppc-libjpeg-turbo' 'ppc-libpng')
makedepends=('cmake' 'libogc2-dkp-toolchain-vars' 'patch' 'ppc-libtiff' 'ppc-libwebp')
options=(!strip libtool staticlibs)
source=("${url}/releases/download/release-${pkgver}/${_pkgname}-${pkgver}.tar.gz"
		"${_pkgname}-${pkgver}.patch")
sha256sums=('2213b56fdaff2220d0e38c8e420cbe1a83c87374190cba8c70af2156097ce30a'
			'c0350a0a30190633e454bdde7689dcab81832962e43cf3f96cad22d64cd9f687')

prepare() {
	patch -Np 0 -i "${_pkgname}-${pkgver}.patch"
}

build() {
	for platform in gamecube wii; do
		cd "${srcdir}/${_pkgname}-${pkgver}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		if [ ${platform} == gamecube ]; then
			powerpc-eabi-cmake -B build-${platform} -DCMAKE_POSITION_INDEPENDENT_CODE=OFF -DSDL2IMAGE_AVIF=OFF -DSDL2IMAGE_DEPS_SHARED=OFF -DSDL2IMAGE_SAMPLES=OFF -DSDL2IMAGE_TIF=OFF -DSDL2IMAGE_WEBP=OFF
		else
			powerpc-eabi-cmake -B build-${platform} -DCMAKE_POSITION_INDEPENDENT_CODE=OFF -DSDL2IMAGE_AVIF=OFF -DSDL2IMAGE_BACKEND_STB=OFF -DSDL2IMAGE_DEPS_SHARED=OFF -DSDL2IMAGE_SAMPLES=OFF -DSDL2IMAGE_TIF=OFF -DSDL2IMAGE_WEBP=OFF
		fi
		powerpc-eabi-cmake -B build-${platform}-full -DCMAKE_POSITION_INDEPENDENT_CODE=OFF -DSDL2IMAGE_AVIF=OFF -DSDL2IMAGE_BACKEND_STB=OFF -DSDL2IMAGE_DEPS_SHARED=OFF -DSDL2IMAGE_SAMPLES=OFF
		cmake --build build-${platform}
		cmake --build build-${platform}-full
	done
}

package_libogc2-sdl2_image() {
	for platform in gamecube wii; do
		cd "${srcdir}/${_pkgname}-${pkgver}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		DESTDIR="${pkgdir}" cmake --install build-${platform}
	done
}

package_libogc2-sdl2_image-full() {
	groups=()
	depends+=('ppc-libtiff' 'ppc-libwebp')
	provides=("libogc2-${_pkgname,,}")
	conflicts=("libogc2-${_pkgname,,}")

	for platform in gamecube wii; do
		cd "${srcdir}/${_pkgname}-${pkgver}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		DESTDIR="${pkgdir}" cmake --install build-${platform}-full
	done
}
