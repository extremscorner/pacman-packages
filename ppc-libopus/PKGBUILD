# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=opus
pkgname="ppc-lib${_pkgname}"
pkgver=1.6.1
pkgrel=1
pkgdesc='Codec designed for interactive speech and audio transmission over the Internet.'
arch=('any')
url='https://www.opus-codec.org/'
license=('BSD-3-Clause')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("https://ftp.osuosl.org/pub/xiph/releases/opus/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('6ffcb593207be92584df15b32466ed64bbec99109f007c82205f0194572411a1')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" -DOPUS_CUSTOM_MODES=ON -DOPUS_FLOAT_APPROX=ON
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 opus.m4 -t "${pkgdir}${PORTLIBS_PREFIX}/share/aclocal"
	install -Dm 644 COPYING -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
