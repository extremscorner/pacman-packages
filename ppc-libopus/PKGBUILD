# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=opus
pkgname="ppc-lib${_pkgname}"
pkgver=1.5.2
pkgrel=1
pkgdesc='Codec designed for interactive speech and audio transmission over the Internet.'
arch=('any')
url='https://www.opus-codec.org/'
license=('BSD-3-Clause')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'devkitppc-cmake' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("https://ftp.osuosl.org/pub/xiph/releases/opus/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('65c1d2f78b9f2fb20082c38cbe47c951ad5839345876e46941612ee87f9a7ce1')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" -DOPUS_CUSTOM_MODES=ON
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 opus.m4 -t "${pkgdir}${PORTLIBS_PREFIX}/share/aclocal"
	install -Dm 644 COPYING -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
