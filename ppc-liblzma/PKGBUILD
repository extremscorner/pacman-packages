# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=xz
pkgname=ppc-liblzma
pkgver=5.8.1
pkgrel=1
pkgdesc='A general-purpose data compression library.'
arch=('any')
url='https://github.com/tukaani-project/xz'
license=('0BSD')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'devkitppc-cmake' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('bdbc23fbf9098843357e71e49685724fda2c320c29cb1b25fd90505f14bb0b3d')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DBUILD_TESTING=OFF -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" -DTUKLIB_CPUCORES_DEFINITIONS= -DTUKLIB_PHYSMEM_DEFINITIONS= -DXZ_DOC=OFF -DXZ_THREADS=no -DXZ_TOOL_LZMADEC=OFF -DXZ_TOOL_LZMAINFO=OFF -DXZ_TOOL_XZ=OFF -DXZ_TOOL_XZDEC=OFF
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 COPYING.0BSD -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
