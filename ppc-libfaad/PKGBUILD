# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=faad2
pkgname="ppc-lib${_pkgname%?}"
pkgver=2.11.2
pkgrel=5
pkgdesc='ISO AAC audio decoder.'
arch=('any')
url='https://github.com/knik0/faad2'
license=('GPL-2.0-or-later')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'libogc2-dkp-toolchain-vars')
provides=("ppc-${_pkgname}")
conflicts=("ppc-${_pkgname}")
replaces=("ppc-${_pkgname}")
options=(!strip libtool staticlibs)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('3fcbd305e4abd34768c62050e18ca0986f7d9c5eca343fb98275418013065c0e')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" -DFAAD_BUILD_CLI=OFF
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	rm -r "${pkgdir}${PORTLIBS_PREFIX}/share"
	install -Dm 644 COPYING -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
