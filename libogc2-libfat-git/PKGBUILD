# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libfat
pkgname="libogc2-${_pkgname}-git"
pkgver=r253.c867be5
pkgrel=1
pkgdesc='FAT library for GameCube and Wii.'
arch=('any')
url="https://github.com/extremscorner/${_pkgname}"
license=('BSD-3-Clause')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC>=r42' 'libogc2')
provides=("libogc2-${_pkgname}")
conflicts=("libogc2-${_pkgname}")
options=(!strip libtool staticlibs)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	make -C "${srcdir}/${_pkgname}" ogc-release
}

package() {
	make -C "${srcdir}/${_pkgname}" DESTDIR="${pkgdir}" ogc-install
}