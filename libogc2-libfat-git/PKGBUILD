# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libfat
pkgname="libogc2-${_pkgname}-git"
pkgver=r271.f587238
pkgrel=2
pkgdesc='FAT library for GameCube and Wii.'
arch=('any')
url='https://github.com/extremscorner/libfat'
license=('BSD-3-Clause')
depends=('devkitPPC>=r42' 'libogc2')
makedepends=('git')
provides=("libogc2-${_pkgname}")
conflicts=("libogc2-${_pkgname}")
options=(!strip libtool staticlibs)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	make ogc-release
}

package() {
	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" make ogc-install
}
