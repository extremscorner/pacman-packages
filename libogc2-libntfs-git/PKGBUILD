# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libntfs
pkgname="libogc2-${_pkgname}-git"
pkgver=r24.fbd5184
pkgrel=2
pkgdesc='Port of the NTFS-3G driver to the Nintendo GameCube/Wii.'
arch=('any')
url='https://github.com/extremscorner/libntfs'
license=('GPL-2.0-or-later')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC>=r42' 'libogc2')
makedepends=('git')
provides=("libogc2-${_pkgname}")
conflicts=("libogc2-${_pkgname}")
options=(!strip libtool staticlibs)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" make install
	mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/wii/share/doc/${_pkgname}"
	cp -r example "${pkgdir}${DEVKITPRO}/libogc2/wii/share/doc/${_pkgname}"
	for platform in gamecube wii; do
		install -Dm 644 LICENSE -t "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/licenses/${_pkgname}"
	done
}
