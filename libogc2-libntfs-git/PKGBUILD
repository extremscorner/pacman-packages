# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libntfs
pkgname="libogc2-${_pkgname}-git"
pkgver=r16.ed1fb27
pkgrel=4
pkgdesc='Port of the NTFS-3G driver to the Nintendo GameCube/Wii.'
arch=('any')
url="https://github.com/extremscorner/${_pkgname}"
license=('GPL-2.0-or-later')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC>=r42' 'libogc2')
makedepends=('git')
provides=("libogc2-${_pkgname}")
conflicts=("libogc2-${_pkgname}")
options=(!strip libtool staticlibs)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" make install
	install -Dm 644 LICENSE -t "${pkgdir}${DEVKITPRO}/libogc2/share/licenses/${_pkgname}"
}