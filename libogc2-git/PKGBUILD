# Maintainer: Extrems <extrems@extremscorner.org>

pkgname=libogc2-git
_pkgname=libogc2
pkgver=r2054.b54f960
pkgrel=2
pkgdesc='C library for GameCube and Wii targeting devkitPPC.'
arch=('any')
url="https://github.com/extremscorner/${_pkgname}"
license=('custom')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC>=r42' 'ppc-libmad')
options=(!strip libtool staticlibs)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	make -C "${srcdir}/${_pkgname}"
}

package() {
	make -C "${srcdir}/${_pkgname}" DESTDIR="${pkgdir}" install
}