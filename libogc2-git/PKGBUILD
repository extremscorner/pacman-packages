# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libogc2
pkgname=(${_pkgname}{,-docs}-git)
pkgver=r2310.b81d6f1
pkgrel=2
pkgdesc='C library for GameCube and Wii targeting devkitPPC.'
arch=('any')
url='https://github.com/extremscorner/libogc2'
license=('custom')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC>=r42' 'gamecube-tools' 'ppc-libmad')
makedepends=('doxygen' 'git')
options=(!strip libtool staticlibs)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	make
	make docs
}

package_libogc2-git() {
	optdepends=(${_pkgname}{-docs-git,-examples})
	provides=("${_pkgname}")
	conflicts=("${_pkgname}")

	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" make install
}

package_libogc2-docs-git() {
	pkgdesc+=' (documentation)'
	depends=("${_pkgname}-git")
	provides=("${_pkgname}-docs")
	conflicts=("${_pkgname}-docs")

	cd "${srcdir}/${_pkgname}"
	for platform in gamecube wii; do
		mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc"
		cp -r docs "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/${_pkgname}"
	done
}
