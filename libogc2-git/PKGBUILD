# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libogc2
pkgname="${_pkgname}-git"
pkgver=r2054.b54f960
pkgrel=4
pkgdesc='C library for GameCube and Wii targeting devkitPPC.'
arch=('any')
url="https://github.com/extremscorner/${_pkgname}"
license=('custom')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC>=r42' 'ppc-libmad')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
options=(!strip libtool staticlibs)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	make DESTDIR="${pkgdir}" install
}