# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libogc2
pkgname=(${_pkgname}{,-docs}-git)
pkgver=r2062.6c699b9
pkgrel=1
pkgdesc='C library for GameCube and Wii targeting devkitPPC.'
arch=('any')
url="https://github.com/extremscorner/${_pkgname}"
license=('custom')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC>=r42' 'gamecube-tools' 'ppc-libmad')
makedepends=('doxygen' 'git')
options=(!strip libtool staticlibs)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	make
	make docs
}

package_libogc2-git() {
	provides=("${_pkgname}")
	conflicts=("${_pkgname}")

	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" make install
}

package_libogc2-docs-git() {
	pkgdesc+=' (documentation)'
	provides=("${_pkgname}-docs")
	conflicts=("${_pkgname}-docs")
	depends=()

	cd "${srcdir}/${_pkgname}"
	mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/share/doc"
	cp -r docs "${pkgdir}${DEVKITPRO}/libogc2/share/doc/${_pkgname}"
}