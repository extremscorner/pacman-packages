# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=glm
pkgname="ppc-${_pkgname}"
pkgver=1.0.3
pkgrel=4
pkgdesc='C++ mathematics library for 3D software based on the OpenGL Shading Language (GLSL) specification.'
arch=('any')
url='https://github.com/g-truc/glm'
license=('MIT')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
		'glm.pc.in')
sha256sums=('6775e47231a446fd086d660ecc18bcd076531cfedd912fbd66e576b118607001'
			'ade588f7f8b1087e4dd78da35a93b3e1bda2a4af8e9e8523a70549a532d3f1bf')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	sed "s/@VERSION@/${pkgver}/g" "${srcdir}/glm.pc.in" > glm.pc
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DCMAKE_INSTALL_DATAROOTDIR="${PORTLIBS_PREFIX}/lib/cmake" -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}"
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 glm.pc -t "${pkgdir}${PORTLIBS_PREFIX}/lib/pkgconfig"
	install -Dm 644 copying.txt -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
