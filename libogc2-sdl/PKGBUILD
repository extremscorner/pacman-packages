# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=SDL
pkgname="libogc2-${_pkgname,,}"
pkgver=1.2.15
pkgrel=14
pkgdesc='A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard.'
arch=('any')
url='https://www.libsdl.org/'
license=('LGPL-2.1-or-later')
groups=('gamecube-portlibs' 'gamecube-sdl-libs' 'wii-portlibs' 'wii-sdl-libs')
depends=('devkitPPC' 'libogc2' 'libogc2-libfat')
makedepends=('git' 'libogc2-dkp-toolchain-vars' 'libogc2-glu' 'libogc2-opengx' 'libogc2-pkg-config' 'ppc-libiconv')
optdepends=('libogc2-glu: OpenGL support'
			'libogc2-opengx: OpenGL support'
			'ppc-libiconv: iconv support')
provides=({gamecube,wii}-${_pkgname,,})
options=(!strip libtool staticlibs)
source=('git+https://github.com/extremscorner/SDL.git#commit=0b44a8a501173856e515824e79b903a3859edd4f')
sha256sums=('7e0db894c21e07ea20594fb685c8ae308728fce4e1d0e6d54272b5bb1372536e')

prepare() {
	cd "${srcdir}/${_pkgname}"
	./autogen.sh
}

build() {
	for platform in gamecube wii; do
		mkdir -p "${srcdir}/${_pkgname}/build-${platform}"
		cd "${srcdir}/${_pkgname}/build-${platform}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		../configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi --enable-nintendo-${platform} ac_cv_func_iconv=yes
		make
	done
}

package() {
	for platform in gamecube wii; do
		cd "${srcdir}/${_pkgname}/build-${platform}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		DESTDIR="${pkgdir}" make install
		rm -r "${pkgdir}${PORTLIBS_PREFIX}/share/man"
		install -Dm 644 ../COPYING -t "${pkgdir}${PORTLIBS_PREFIX}/share/licenses/${_pkgname}"
	done
}
