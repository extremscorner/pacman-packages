# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libpng
pkgname="ppc-${_pkgname}-apng"
pkgver=1.6.47
pkgrel=1
pkgdesc='A collection of routines used to create PNG format graphics files.'
arch=('any')
url='https://sourceforge.net/projects/libpng-apng/'
license=('libpng-2.0')
groups=('ppc-portlibs')
depends=('devkitPPC' 'ppc-zlib')
makedepends=('dkp-toolchain-vars')
provides=("ppc-${_pkgname}")
conflicts=("ppc-${_pkgname}")
options=(!strip libtool staticlibs)
source=("https://downloads.sourceforge.net/sourceforge/libpng/${_pkgname}-${pkgver}.tar.xz"
		"https://downloads.sourceforge.net/sourceforge/libpng-apng/${_pkgname}-${pkgver}-apng.patch.gz")
sha256sums=('b213cb381fbb1175327bd708a77aab708a05adde7b471bc267bd15ac99893631'
			'5b086fafe7e12784b28693e19a5bcf68677a8c52947115712a56c3d123944f6f')

prepare() {
	patch -Np 0 -i "${_pkgname}-${pkgver}-apng.patch"
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/ppcvars.sh"
	./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi --disable-tests --disable-tools
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/ppcvars.sh"
	DESTDIR="${pkgdir}" make install LN_S='cp -pR'
	install -Dm 644 LICENSE -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
