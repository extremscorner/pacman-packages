# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libpng
pkgname="ppc-${_pkgname}-apng"
pkgver=1.6.53
pkgrel=1
pkgdesc='A collection of routines used to create PNG format graphics files. (with APNG support)'
arch=('any')
url='https://sourceforge.net/projects/libpng-apng/'
license=('libpng-2.0')
groups=('ppc-portlibs')
depends=('devkitPPC' 'ppc-zlib')
makedepends=('libogc2-dkp-toolchain-vars' 'patch')
provides=("ppc-${_pkgname}")
conflicts=("ppc-${_pkgname}")
options=(!strip libtool staticlibs)
source=("https://downloads.sourceforge.net/sourceforge/libpng/${_pkgname}-${pkgver}.tar.xz"
		"https://downloads.sourceforge.net/sourceforge/libpng-apng/${_pkgname}-${pkgver}-apng.patch.gz")
sha256sums=('1d3fb8ccc2932d04aa3663e22ef5ef490244370f4e568d7850165068778d98d4'
			'452a1a290bd0cf18737fad0057dc17b7fdf10a73eda2d6d4f31ba04fda25ef2c')

prepare() {
	patch -Np 0 -i "${_pkgname}-${pkgver}-apng.patch"
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi --disable-tests --disable-tools
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" make install LN_S='cp -pR'
	rm -r "${pkgdir}${PORTLIBS_PREFIX}/share"
	install -Dm 644 LICENSE -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
