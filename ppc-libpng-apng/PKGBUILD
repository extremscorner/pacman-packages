# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libpng
pkgname="ppc-${_pkgname}-apng"
pkgver=1.6.50
pkgrel=1
pkgdesc='A collection of routines used to create PNG format graphics files. (with APNG support)'
arch=('any')
url='https://sourceforge.net/projects/libpng-apng/'
license=('libpng-2.0')
groups=('ppc-portlibs')
depends=('devkitPPC' 'ppc-zlib')
makedepends=('dkp-toolchain-vars')
provides=("ppc-${_pkgname}")
conflicts=("ppc-${_pkgname}")
options=(!strip libtool staticlibs)
source=("https://downloads.sourceforge.net/sourceforge/libpng/${_pkgname}-${pkgver}.tar.xz"
		"https://downloads.sourceforge.net/sourceforge/libpng-apng/${_pkgname}-${pkgver}-apng.patch.gz")
sha256sums=('4df396518620a7aa3651443e87d1b2862e4e88cad135a8b93423e01706232307'
			'687ddc0c7cb128a3ea58e159b5129252537c27ede0c32a93f11f03127f0c0165')

prepare() {
	patch -Np 0 -i "${_pkgname}-${pkgver}-apng.patch"
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/ppcvars.sh"
	./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi --disable-tests --disable-tools
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/ppcvars.sh"
	DESTDIR="${pkgdir}" make install LN_S='cp -pR'
	install -Dm 644 LICENSE -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
