# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libxmp
pkgname="ppc-${_pkgname}"
pkgver=4.7.0
pkgrel=1
pkgdesc='A library that renders module files to PCM data.'
arch=('any')
url='https://github.com/libxmp/libxmp'
license=('MIT')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("${url}/archive/refs/tags/${_pkgname}-${pkgver}.tar.gz")
noextract=("${_pkgname}-${pkgver}.tar.gz")
sha256sums=('be5c6f76528b274e05c17f97354eda2872939fc1b6f4a8f0dd10c3a17ee65482')

prepare() {
	tar -xf "${_pkgname}-${pkgver}.tar.gz" --one-top-level --strip-components=1
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DBUILD_LITE=ON -DBUILD_SHARED=OFF -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}"
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 docs/COPYING -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
