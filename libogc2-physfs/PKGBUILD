# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=physfs
pkgname="libogc2-${_pkgname}"
pkgver=3.2.0
pkgrel=2
pkgdesc='A library to provide abstract access to various archives.'
arch=('any')
url='https://github.com/icculus/physfs'
license=('Zlib')
groups=('gamecube-portlibs' 'wii-portlibs')
depends=('devkitPPC' 'libogc2')
makedepends=('cmake' 'git' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("git+${url}.git#tag=release-${pkgver}")
sha256sums=('2a7313be69c0f381ec48b07fefbe32a987421504319f26c2ac0fcf22e510b714')

prepare() {
	cd "${srcdir}/${_pkgname}"
	git cherry-pick -n f7d24ce8486d9229207cca1ff98858fe60ffe583
	git cherry-pick -n 741c4be358cd87366af1387d1e04e3f7d4f50c1e
}

build() {
	for platform in gamecube wii; do
		cd "${srcdir}/${_pkgname}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		powerpc-eabi-cmake -B build-${platform} -DPHYSFS_BUILD_DOCS=OFF -DPHYSFS_BUILD_SHARED=OFF -DPHYSFS_BUILD_TEST=OFF
		cmake --build build-${platform}
	done
}

package() {
	for platform in gamecube wii; do
		cd "${srcdir}/${_pkgname}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		DESTDIR="${pkgdir}" cmake --install build-${platform}
		install -Dm 644 LICENSE.txt -t "${pkgdir}${PORTLIBS_PREFIX}/share/licenses/${_pkgname}"
	done
}
