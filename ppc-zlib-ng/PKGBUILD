# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=zlib-ng
pkgname=(ppc-${_pkgname}{,-compat})
pkgver=2.3.2
pkgrel=2
pkgdesc='zlib replacement with optimizations for "next generation" systems.'
arch=('any')
url='https://github.com/zlib-ng/zlib-ng'
license=('Zlib')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('6a0561b50b8f5f6434a6a9e667a67026f2b2064a1ffa959c6b2dae320161c2a8')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DBUILD_TESTING=OFF -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" -DWITH_RUNTIME_CPU_DETECTION=OFF
	powerpc-eabi-cmake -B build-compat -DBUILD_TESTING=OFF -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" -DWITH_RUNTIME_CPU_DETECTION=OFF -DZLIB_COMPAT=ON
	cmake --build build
	cmake --build build-compat
}

package_ppc-zlib-ng() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 LICENSE.md -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}

package_ppc-zlib-ng-compat() {
	pkgdesc+=' (zlib compat)'
	provides=('ppc-zlib')
	conflicts=('ppc-zlib')
	replaces=('ppc-zlib')

	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build-compat
	install -Dm 644 LICENSE.md -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
