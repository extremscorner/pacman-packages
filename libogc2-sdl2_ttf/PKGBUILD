# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=SDL2_ttf
pkgname="libogc2-${_pkgname,,}"
pkgver=2.24.0
pkgrel=3
pkgdesc='A library that allows you to use TrueType fonts in your SDL applications. (Version 2)'
arch=('any')
url='https://github.com/libsdl-org/SDL_ttf'
license=('Zlib')
groups=('gamecube-portlibs' 'gamecube-sdl2-libs' 'wii-portlibs' 'wii-sdl2-libs')
depends=('devkitPPC' 'libogc2-sdl2' 'ppc-freetype')
makedepends=('cmake' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("${url}/releases/download/release-${pkgver}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('0b2bf1e7b6568adbdbc9bb924643f79d9dedafe061fa1ed687d1d9ac4e453bfd')

build() {
	for platform in gamecube wii; do
		cd "${srcdir}/${_pkgname}-${pkgver}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		powerpc-eabi-cmake -B build-${platform} -DCMAKE_POSITION_INDEPENDENT_CODE=OFF -DSDL2TTF_SAMPLES=OFF
		cmake --build build-${platform}
	done
}

package() {
	for platform in gamecube wii; do
		cd "${srcdir}/${_pkgname}-${pkgver}"
		source "${DEVKITPRO}/libogc2/${platform}vars.sh"
		DESTDIR="${pkgdir}" cmake --install build-${platform}
	done
}
