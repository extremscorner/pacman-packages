# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libpng
pkgname="ppc-${_pkgname}"
pkgver=1.6.55
pkgrel=1
pkgdesc='A collection of routines used to create PNG format graphics files.'
arch=('any')
url='https://www.libpng.org/pub/png/libpng.html'
license=('libpng-2.0')
groups=('ppc-portlibs')
depends=('devkitPPC' 'ppc-zlib')
makedepends=('libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("https://downloads.sourceforge.net/sourceforge/libpng/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('d925722864837ad5ae2a82070d4b2e0603dc72af44bd457c3962298258b8e82d')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi --disable-tests --disable-tools
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" make install LN_S='cp -pR'
	rm -r "${pkgdir}${PORTLIBS_PREFIX}/share"
	install -Dm 644 LICENSE -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
