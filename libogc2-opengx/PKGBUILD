# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=opengx
pkgname=({gamecube,wii}-${_pkgname})
pkgver=0.15.0
pkgrel=1
epoch=1
pkgdesc='OpenGL-like wrapper for Nintendo Wii/GameCube.'
arch=('any')
url='https://github.com/extremscorner/opengx'
license=('BSD-3-Clause')
depends=('devkitPPC' 'libogc2')
makedepends=('catnip' 'cmake' 'gamecube-cmake' 'git' 'libogc2-cmake' 'wii-cmake')
options=(!strip libtool staticlibs !buildflags)
source=("git+${url}.git#commit=96a47f79ce39c715e375c1c912ea41e7ead60e71")
sha256sums=('SKIP')

build() {
	cd "${srcdir}/${_pkgname}"
	catnip build
}

package_gamecube-opengx() {
	groups=('gamecube-portlibs')

	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" catnip install gamecube
}

package_wii-opengx() {
	groups=('wii-portlibs')

	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" catnip install wii
}
