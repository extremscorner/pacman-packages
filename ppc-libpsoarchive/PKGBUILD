# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libpsoarchive
pkgname="ppc-${_pkgname}"
pkgver=r20.53113b3
pkgrel=5
pkgdesc='Library for reading and writing various archive formats used in the Phantasy Star Online series of games.'
arch=('any')
url='https://github.com/extremscorner/libpsoarchive'
license=('LGPL-2.1-only' 'LGPL-3.0-only')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('dkp-toolchain-vars' 'git')
options=(!strip libtool staticlibs)
source=("git+${url}.git#commit=53113b3347c9e3b650b1278b06ba8950fc4b3d23")
sha256sums=('SKIP')

prepare() {
	cd "${srcdir}/${_pkgname}"
	./autogen.sh
}

build() {
	cd "${srcdir}/${_pkgname}"
	source "${DEVKITPRO}/ppcvars.sh"
	./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	source "${DEVKITPRO}/ppcvars.sh"
	DESTDIR="${pkgdir}" make install
	install -Dm 644 COPYING* -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
