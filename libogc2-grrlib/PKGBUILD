# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=GRRLIB
pkgname=(libogc2-${_pkgname,,}{,-docs,-examples})
pkgver=4.6.1
pkgrel=10
pkgdesc='C/C++ 2D/3D graphics library for Wii application developers.'
arch=('any')
url='https://grrlib.santo.fr/'
license=('MIT')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC' 'libogc2' 'libogc2-libfat' 'ppc-freetype' 'ppc-libjpeg-turbo' 'ppc-libpng')
makedepends=('catnip' 'cmake' 'doxygen' 'git' 'libogc2-cmake')
options=(!strip libtool staticlibs !buildflags)
source=('git+https://github.com/extremscorner/GRRLIB.git#commit=71ece7ddfcbb4a6a15294898f70c4d026a238b5e')
sha256sums=('9c038e07ae5a5f4d80e73e647e657853c6340e0e31ca6cb0d6afff3fe3e94c9d')

build() {
	cd "${srcdir}/${_pkgname}"
	catnip build
	doxygen grrlib.doxygen
}

package_libogc2-grrlib() {
	optdepends=(${pkgbase}{-docs,-examples})

	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" catnip install
	for platform in gamecube wii; do
		install -Dm 644 LICENCE.md -t "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/licenses/${_pkgname}"
	done
}

package_libogc2-grrlib-docs() {
	pkgdesc+=' (documentation)'
	depends=("${pkgbase}")

	cd "${srcdir}/${_pkgname}"
	for platform in gamecube wii; do
		mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/${_pkgname}"
		cp -r {html,latex} "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/${_pkgname}"
	done
}

package_libogc2-grrlib-examples() {
	pkgdesc+=' (examples)'
	depends=("${pkgbase}")

	cd "${srcdir}/${_pkgname}"
	for platform in gamecube wii; do
		mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/${_pkgname}"
	done
	mv examples/gamecube "${pkgdir}${DEVKITPRO}/libogc2/gamecube/share/doc/${_pkgname}/examples"
	cp -r examples "${pkgdir}${DEVKITPRO}/libogc2/wii/share/doc/${_pkgname}"
}
