# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=tinyxml2
pkgname="ppc-${_pkgname}"
pkgver=11.0.0
pkgrel=1
pkgdesc='A simple, small and efficient C++ XML parser.'
arch=('any')
url='https://github.com/leethomason/tinyxml2'
license=('Zlib')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'devkitppc-cmake' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('5556deb5081fb246ee92afae73efd943c889cef0cafea92b0b82422d6a18f289')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DBUILD_TESTING=OFF -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}"
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 LICENSE.txt -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
