# Maintainer: Extrems <extrems@extremscorner.org>

_target=aarch64-linux-gnu
_pkgname=FreeImage
pkgname="${_target}-${_pkgname,,}-svn"
pkgver=r1911
pkgrel=2
pkgdesc='Library project for developers who would like to support popular graphics image formats.'
arch=('any')
url='https://freeimage.sourceforge.io/'
license=('FreeImage' 'GPL-2.0-or-later')
makedepends=("${_target}-gcc" 'dos2unix' 'subversion')
provides=("${_target}-${_pkgname,,}")
conflicts=("${_target}-${_pkgname,,}")
options=(!strip !buildflags staticlibs)
source=("${_pkgname}::svn+https://svn.code.sf.net/p/freeimage/svn/FreeImage/trunk"
		'LibJXR.patch'
		'LibPNG.patch'
		'ZLib.patch')
sha256sums=('SKIP'
			'a481ef7127c4676c79c5d4bc1f41913e83d206026784d084ff2722b824708942'
			'e0418d232ef3af1bf02527de96a153c1846c05b884250761cfe35e2dacdab5cf'
			'70a3a5470b106e6bf36d43f51c84b33ada748ad4002757fb13aa6c7f33ecaab2')

prepare() {
	cd "${srcdir}/${_pkgname}"
	svn patch "${srcdir}/LibJXR.patch"
	svn patch "${srcdir}/LibPNG.patch"
	svn patch "${srcdir}/ZLib.patch"
	sh gensrclist.sh
	sh genfipsrclist.sh
}

pkgver() {
	cd "${srcdir}/${_pkgname}"
	local ver="$(svnversion)"
	printf "r%s" "${ver//[[:alpha:]]}"
}

build() {
	cd "${srcdir}/${_pkgname}"
	export CC="${_target}-gcc"
	export CXX="${_target}-g++"
	export AR="${_target}-ar"
	make -f Makefile.gnu
	make -f Makefile.fip
}

package() {
	cd "${srcdir}/${_pkgname}"
	export DESTDIR="${pkgdir}"
	export INCDIR="${pkgdir}/usr/${_target}/include"
	export INSTALLDIR="${pkgdir}/usr/${_target}/lib"
	make -f Makefile.gnu install
	make -f Makefile.fip install
	install -Dm 644 license*.txt -t "${pkgdir}/usr/${_target}/share/licenses/${_pkgname,,}"
}
