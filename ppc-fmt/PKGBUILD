# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=fmt
pkgname="ppc-${_pkgname}"
pkgver=12.0.0
pkgrel=1
pkgdesc='A modern formatting library for C++.'
arch=('any')
url='https://github.com/fmtlib/fmt'
license=('MIT')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'devkitppc-cmake' 'dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('aa3e8fbb6a0066c03454434add1f1fc23299e85758ceec0d7d2d974431481e40')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/ppcvars.sh"
	powerpc-eabi-cmake -B build -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" -DFMT_TEST=OFF
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 LICENSE -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
