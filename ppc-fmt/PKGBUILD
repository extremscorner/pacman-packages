# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=fmt
pkgname="ppc-${_pkgname}"
pkgver=12.1.0
pkgrel=3
pkgdesc='A modern formatting library for C++.'
arch=('any')
url='https://github.com/fmtlib/fmt'
license=('MIT')
groups=('ppc-portlibs')
depends=('devkitPPC')
makedepends=('cmake' 'libogc2-dkp-toolchain-vars')
options=(!strip libtool staticlibs)
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('ea7de4299689e12b6dddd392f9896f08fb0777ac7168897a244a6d6085043fea')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	powerpc-eabi-cmake -B build -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" -DFMT_TEST=OFF
	cmake --build build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	source "${DEVKITPRO}/libogc2/ppcvars.sh"
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm 644 LICENSE -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
}
