# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=SDL2
pkgname="libogc2-${_pkgname,,}"
pkgver=2.28.5
pkgrel=11
pkgdesc='A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard. (Version 2)'
arch=('any')
url='https://www.libsdl.org/'
license=('Zlib')
groups=('gamecube-portlibs' 'gamecube-sdl2-libs' 'wii-portlibs' 'wii-sdl2-libs')
depends=('devkitPPC' 'libogc2' 'libogc2-libfat')
makedepends=('catnip' 'cmake' 'git' 'libogc2-cmake' 'libogc2-opengx')
optdepends=('libogc2-opengx: OpenGL support')
provides=({gamecube,wii}-${_pkgname,,})
options=(!strip libtool staticlibs !buildflags)
source=('git+https://github.com/extremscorner/SDL.git#commit=bd712e858e892e65f4f4f641ac6280cafdbbca5b')
sha256sums=('d52512c4bc06e13e75d74def79ab55b3b89be88f948fab6ff140877753fa054c')

build() {
	cd "${srcdir}/${_pkgname%?}"
	catnip build -DSDL_SYSTEM_ICONV=OFF
}

package() {
	cd "${srcdir}/${_pkgname%?}"
	DESTDIR="${pkgdir}" catnip install
}
