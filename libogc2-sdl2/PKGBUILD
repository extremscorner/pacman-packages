# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=SDL2
pkgname="libogc2-${_pkgname,,}"
pkgver=2.28.5
pkgrel=2
pkgdesc='A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard. (Version 2)'
arch=('any')
url='https://www.libsdl.org/'
license=('Zlib')
groups=('gamecube-portlibs' 'gamecube-sdl2-libs' 'wii-portlibs' 'wii-sdl2-libs')
depends=('devkitPPC' 'libogc2' 'libogc2-libfat')
makedepends=('catnip' 'cmake' 'gamecube-cmake' 'git' 'libogc2-cmake' 'libogc2-opengx' 'wii-cmake')
optdepends=('libogc2-opengx: OpenGL support')
provides=({gamecube,wii}-${_pkgname,,})
conflicts=({gamecube,wii}-${_pkgname,,})
options=(!strip libtool staticlibs !buildflags)
source=("git+https://github.com/extremscorner/SDL.git#commit=b4dbded720b4ddbcf88b3d804063613fa0e4ad98")
sha256sums=('SKIP')

build() {
	cd "${srcdir}/${_pkgname%?}"
	catnip build
}

package() {
	cd "${srcdir}/${_pkgname%?}"
	DESTDIR="${pkgdir}" catnip install
}
