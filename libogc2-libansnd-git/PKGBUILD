# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libansnd
pkgname="libogc2-${_pkgname}-git"
pkgver=r5.9e59fc4
pkgrel=1
pkgdesc='Another Sound Library for Wii and GameCube homebrew.'
arch=('any')
url='https://github.com/extremscorner/libansnd'
license=('Zlib')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC' 'libogc2')
makedepends=('catnip' 'cmake' 'gamecube-cmake' 'gamecube-tools-git' 'git' 'libogc2-cmake' 'wii-cmake')
provides=("libogc2-${_pkgname}")
conflicts=("libogc2-${_pkgname}")
options=(!strip libtool staticlibs !buildflags)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	catnip build -DBUILD_EXAMPLES=OFF
}

package() {
	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" catnip install
	mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/share/doc/${_pkgname}"
	cp -r examples "${pkgdir}${DEVKITPRO}/libogc2/share/doc/${_pkgname}"
	install -Dm 644 LICENSE.md -t "${pkgdir}${DEVKITPRO}/libogc2/share/licenses/${_pkgname}"
}
