# Maintainer: Extrems <extrems@extremscorner.org>

_pkgname=libansnd
pkgname=(libogc2-${_pkgname}{,-examples}-git)
pkgver=r24.d76e007
pkgrel=2
pkgdesc='Another Sound Library for Wii and GameCube homebrew.'
arch=('any')
url='https://github.com/extremscorner/libansnd'
license=('Zlib')
groups=('gamecube-dev' 'wii-dev')
depends=('devkitPPC' 'libogc2')
makedepends=('catnip' 'cmake' 'gamecube-tools-git' 'git' 'libogc2-cmake')
options=(!strip libtool staticlibs !buildflags)
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	catnip build -DBUILD_EXAMPLES=OFF
}

package_libogc2-libansnd-git() {
	optdepends=("libogc2-${_pkgname}-examples-git")
	provides=("libogc2-${_pkgname}")
	conflicts=("libogc2-${_pkgname}")

	cd "${srcdir}/${_pkgname}"
	DESTDIR="${pkgdir}" catnip install
	for platform in gamecube wii; do
		install -Dm 644 LICENSE.md -t "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/licenses/${_pkgname}"
	done
}

package_libogc2-libansnd-examples-git() {
	pkgdesc+=' (examples)'
	depends=("libogc2-${_pkgname}-git" 'libogc2-cmake' 'ppc-libvorbisidec')
	provides=("libogc2-${_pkgname}-examples")
	conflicts=("libogc2-${_pkgname}-examples")

	cd "${srcdir}/${_pkgname}"
	for platform in gamecube wii; do
		mkdir -p "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/${_pkgname}"
		cp -r examples "${pkgdir}${DEVKITPRO}/libogc2/${platform}/share/doc/${_pkgname}"
	done
}
